************************************************************************
*  case PIN_A                                                          *
*  295-group JEF3.1 DRAGLIB                                            *
*  DISTRIBUTED SELF-SHIELDING                                          *
*  Author: A. Hebert                                                   *
************************************************************************

! STRUCTURES and MODULES

LINKED_LIST GEOM TRACK_SS TRACK LIBRARY LIBRARY2 SYS FLUX
  StepList BURNUP EDIOBJ ;

SEQ_ASCII UOX_TBH ;
SEQ_BINARY TF_EXC ;

SEQ_ASCII FIG.ps :: FILE './PIN_FIG.ps' ;

SEQ_ASCII _BURN :: FILE './_BURN_rowland.txt' ;
SEQ_ASCII _LIBR :: FILE './_LIB_rowland.txt' ;
SEQ_ASCII _EDIT :: FILE './_EDIT_rowland.txt' ;

MODULE LIB: GEO: SYBILT: G2S: SALT: MAC: USS:
  ASM: FLU: DELETE: UTL: EVO: GREP:
  EDI: END: ;

INTEGER COMB0101 COMB0105 COMB0106 COMB0107 GAP2 GAIN3 MODE4 :=
        1 5 6 7 2 3 4 ; !  DISTRIBUTED SELF-SHIELDING 

REAL N_Zr90       N_Zr91      N_Zr92       N_Zr94       N_Zr96       :=
     2.165762E-02 4.72300E-03 7.219212E-03 7.316029E-03 1.178642E-03 ;

INTEGER an2d := 12 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 20.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

REAL RCOMB1 RCOMB5 RCOMB6 RCOMB7 ;

EVALUATE RCOMB7 := 0.4096 ;
EVALUATE RCOMB1 := 0.5 SQRT RCOMB7 * ;
EVALUATE RCOMB5 := 0.8 SQRT RCOMB7 * ;
EVALUATE RCOMB6 := 0.95 SQRT RCOMB7 * ;

REAL Kinf1 Keff1 ;

! GEOMETRY

GEOM := GEO: :: CARCEL 6
  X- REFL X+ REFL
  Y- REFL Y+ REFL
  RADIUS 0.0 <<RCOMB1>> <<RCOMB5>>
             <<RCOMB6>> <<RCOMB7>> 0.4180 0.4750
  MIX <<COMB0101>>  <<COMB0105>>
      <<COMB0106>> <<COMB0107>> <<GAP2>> <<GAIN3>> <<MODE4>>
  MESHX 0.0 1.2600 MESHY 0.0 1.2600
;

! TRACKING

TRACK_SS := SYBILT: GEOM ::
  TITLE 'PIN_A BENCHMARK'
  MAXR 20 QUA2 20 3 DP01 MAXZ 200000 ;

UOX_TBH FIG.ps := G2S: GEOM :: DRAWNOD ;

TRACK TF_EXC := SALT: UOX_TBH ::
  EDIT 3
  ALLG
  TSPC <<an2d>> <<densur>> REND
;

! LIBRARY

LIBRARY := LIB: ::
  EDIT 1
  NMIX 7    ! MAXIMUM OF MATERIAL MIXTURES
  PT
  CTRA APOL ! APOLLO TYPE TRANSPORT CORRECTION
  ANIS 2
  ADED 4 NELAS N4N N2N N3N
  CALENDF 3 PT      ! CALENDF TYPE PROBABILITY TABLES

  DEPL LIB: DRAGON FIL: DLIB_295

  MIXS LIB: DRAGON FIL: DLIB_295

  MIX <<COMB0101>> 900.0
    O16     = O16    4.48565E-2
    U235    = U235   4.08785E-4 1 IRSET PT 1
    U238    = U238   2.20157E-2 1 IRSET PT 1
  MIX <<COMB0105>> COMB <<COMB0101>> 1.0
  MIX <<COMB0106>> COMB <<COMB0101>> 1.0
  MIX <<COMB0107>> COMB <<COMB0101>> 1.0
  MIX <<GAP2>> 600.0 NOEV
    O16     = O16    3.76439E-6
  MIX <<GAIN3>> 600.0 NOEV
    Zr90    = Zr90   <<N_Zr90>> 2 IRSET PT 1
    Zr91    = Zr91   <<N_Zr91>> 2 IRSET PT 1
    Zr92    = Zr92   <<N_Zr92>> 2 IRSET PT 1
    Zr94    = Zr94   <<N_Zr94>>
    Zr96    = Zr96   <<N_Zr96>>
  MIX <<MODE4>> 600.0 NOEV
    H1      = H1_H2O 4.69585E-2
    O16     = O16    2.34793E-2
    B10     = B10    5.53089E-6
    B11     = B11    2.18498E-5
;

! SELF-SHIELDING & FLUX

LIBRARY2 := USS: LIBRARY TRACK_SS :: EDIT 1 TRAN PASS 3 GRMIN 52 ;
SYS := ASM: LIBRARY2 TRACK TF_EXC :: EDIT 1 PIJ ;
FLUX := FLU: SYS LIBRARY2 TRACK :: EDIT 1 TYPE K ;

! EDITION

EDIOBJ := EDI: FLUX LIBRARY2 TRACK ::
  EDIT 5
  MICR 11 U235 U238 Pu238 Pu239
    Pu240 Pu241 Pu242 Am241 Am243
    Cm242 Cm244
  TAKE MIX 1 0 0 0 1 1 1
  COND
  SAVE ON FUEL
;

! BURN UP

REAL    Norm_f2 := 39.0 ; ! POWER NORM 39W/g
INTEGER istep :=  1 ;
INTEGER nstep := 60 ;
INTEGER nauto := 60 ;
INTEGER iauto :=  1 ;
INTEGER valstep valauto ;
REAL    evobeg evoend step2 stepauto ;

StepList := UTL: :: CREA 'ListBU' <<nstep>> =
    30.00     50.00     75.00    150.00    250.00
   500.00    750.00   1000.00   2000.00   2500.00
  3000.00   3500.00   4000.00   4500.00   5000.00
  5500.00   6000.00   6500.00   7000.00   7500.00
  8000.00   8500.00   9000.00   9500.00  10000.00
 11000.00  12000.00  13000.00  14000.00  15000.00
 16000.00  17000.00  18000.00  19000.00  20000.00
 22000.00  24000.00  26000.00  28000.00  30000.00
 32000.00  34000.00  36000.00  38000.00  40000.00
 42000.00  44000.00  46000.00  48000.00  50000.00
 52000.00  54000.00  56000.00  58000.00  60000.00
 62000.00  64000.00  66000.00  68000.00  70000.00
;

StepList := UTL: StepList :: CREA 'ListAutop' <<nauto>> =
    30.00     50.00     75.00    150.00    250.00
   500.00    750.00   1000.00   2000.00   2500.00
  3000.00   3500.00   4000.00   4500.00   5000.00
  5500.00   6000.00   6500.00   7000.00   7500.00
  8000.00   8500.00   9000.00   9500.00  10000.00
 11000.00  12000.00  13000.00  14000.00  15000.00
 16000.00  17000.00  18000.00  19000.00  20000.00
 22000.00  24000.00  26000.00  28000.00  30000.00
 32000.00  34000.00  36000.00  38000.00  40000.00
 42000.00  44000.00  46000.00  48000.00  50000.00
 52000.00  54000.00  56000.00  58000.00  60000.00
 62000.00  64000.00  66000.00  68000.00  70000.00
;

GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Keff1<< ;
ECHO "+++ Burnup=" 0.0 " Keff=" Keff1 ;

EVALUATE evoend := 0. ;
WHILE istep nstep <= DO

  GREP: StepList :: GETVAL 'ListBU' <<istep>> >>step2<< ;

  EVALUATE evobeg := evoend ;
  EVALUATE evoend := step2 Norm_f2 / ;

  ECHO "BURNUP step" istep "between" evobeg "and" evoend "day:" ;

  IF istep 1 = THEN
    BURNUP LIBRARY2 := EVO: LIBRARY2 FLUX TRACK ::
      EDIT 2
      DEPL <<evobeg>> <<evoend>> DAY POWR <<Norm_f2>>
      KAPS EXTR NSAT NODI
      GLOB
    ;

  ELSE
    BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK ::
      EDIT 2
      DEPL <<evobeg>> <<evoend>> DAY POWR <<Norm_f2>>
      KAPS EXTR NSAT NODI
      GLOB
    ;

  ENDIF ;

  GREP: StepList :: GETVAL 'ListAutop' <<iauto>> >>stepauto<< ;
  EVALUATE valstep := step2 R_TO_I ;
  EVALUATE valauto := stepauto R_TO_I ;

  IF valstep valauto = THEN

    ECHO "StepAuto:" stepauto "MWj/t" ;
    ECHO "Self-shielding calculation" istep "at" evoend "DAY:" ;

    LIBRARY2 := USS: LIBRARY LIBRARY2 TRACK_SS ::
      EDIT 1 TRAN PASS 3 GRMIN 52 ;

  ENDIF ;

  IF iauto nauto < THEN
    EVALUATE iauto := iauto 1 + ;
  ENDIF ;

  ECHO "step2=" step2 "evoend=" evoend ;

  SYS := DELETE: SYS ;
  SYS := ASM: LIBRARY2 TRACK TF_EXC :: EDIT 1 PIJ ;

  FLUX := FLU: FLUX SYS LIBRARY2 TRACK :: EDIT 1 TYPE K ;

  EDIOBJ := EDI: EDIOBJ FLUX LIBRARY2 TRACK ::
    EDIT 5
    MICR 11 U235 U238 Pu238 Pu239
      Pu240 Pu241 Pu242 Am241 Am243
      Cm242 Cm244
    TAKE MIX 1 0 0 0 1 1 1
    COND
    SAVE ON FUEL
  ;

  GREP: FLUX :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf1<<
    GETVAL 'K-EFFECTIVE ' 1 1 1 >>Keff1<< ;

  ECHO "+++ Burnup=" step2 " Keff=" Keff1 ;

  BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK :: EDIT 2
    SAVE <<evoend>> DAY POWR <<Norm_f2>> ;

  EVALUATE istep := istep 1 + ;

ENDWHILE ;

_BURN := BURNUP ;
_LIBR := LIBRARY2 ;
_EDIT := EDIOBJ ;

ECHO "PIN_A completed" ;

END: ;
QUIT .
