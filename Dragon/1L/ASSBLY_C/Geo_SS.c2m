*DECK Geo_SS
*----
*  Name          : Geo_SS.c2m
*  Type          : DRAGON procedure
*  Use           : Geometry generation for Self-Shielding Calculation
*                  of the CASEB assembly
*  Author        : A. Hebert
*
*  Procedure called as:
*
*GEOSS := Geo_SS :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
*                   <<R_int_TG>> <<R_ext_TG>> <<R_int_TI>>
*                   <<R_ext_TI>> <<R_int_G>> <<R_ext_G>> <<Cote>>
*                   <<CoteL>> ;
*
*  Input data   :
*    Rcomb1     :  50/100 of outer radius of fuel (cm)
*    Rcomb2     :  80/100 of outer radius of fuel (cm)
*    Rcomb3     :  95/100 of outer radius of fuel (cm)
*    Rcomb4     : 100/100 of outer radius of fuel (cm)
*    R_int_TG   : Inner radius of cladding of guide tube (cm)
*    R_ext_TG   : Outer radius of cladding of guide tube (cm)
*    R_int_TI   : Inner radius of cladding of guide tube (cm)
*    R_ext_TI   : Outer radius of cladding of guide tube (cm)
*    R_int_G    : Inner radius of cladding of fuel tube (cm)
*    R_ext_G    : Outer radius of cladding of fuel tube (cm)
*    Cote       : Lattice pitch (cm)
*    CoteL      : Lattice pitch + Water space (cm)
*    Rcomb1Gad  : 1st Gd pin radius (cm)
*    Rcomb1Gad  : 2nd Gd pin radius (cm)
*    Rcomb1Gad  : 3rd Gd pin radius (cm)
*    Rcomb1Gad  : 4th Gd pin radius (cm)
*    Rcomb1Gad  : 5th Gd pin radius (cm)
*    Rcomb1Gad  : 6th Gd pin radius (cm)
*
*  Output data  :
*    GEOSS      : Geometry for Self-Shielding Calculation

PARAMETER  GEOSS  ::
       EDIT 0
           ::: LINKED_LIST GEOSS  ;
   ;
*----
*  Modules used in this procedure
*----
MODULE  GEO: END: ;

*----
*  Input data recovery
*----
*
REAL Rcomb1       Rcomb2       Rcomb3       Rcomb4     ;
:: >>Rcomb1<<   >>Rcomb2<<   >>Rcomb3<<   >>Rcomb4<<   ;
REAL R_int_TG     R_ext_TG     R_int_TI     R_ext_TI   ;
:: >>R_int_TG<< >>R_ext_TG<< >>R_int_TI<< >>R_ext_TI<< ;
REAL R_int_G      R_ext_G      Cote         CoteL      ;
:: >>R_int_G<<  >>R_ext_G<<  >>Cote<<     >>CoteL<<    ;
REAL Rcomb1Gad     Rcomb2Gad     Rcomb3Gad             ;
:: >>Rcomb1Gad<< >>Rcomb2Gad<< >>Rcomb3Gad<<           ;
REAL Rcomb4Gad       Rcomb5Gad       Rcomb6Gad         ;
:: >>Rcomb4Gad<<   >>Rcomb5Gad<<   >>Rcomb6Gad<<       ;


GEOSS := GEO: :: CAR2D 9 9
  EDIT 0
  X- DIAG X+ REFL
  Y- SYME Y+ DIAG
  CELL
  TI C0201 C0301     TG  C0501  C0601     TG   C0801 C0901
     C0202 C0302  C0402  C0502  C0602  C0702   C0802 C0902
           C0303  C0403  C0503  G0603  C0703   C0803 C0903
                     TG  C0504  C0604     TG   C0804 C0904
                         C0505  C0605  C0705   C0805 C0905
                                   TG  C0706   C0806 C0906
                                       G0707   C0807 C0907
                                               C0808 C0908
                                                     C0909



  ::: C0201 := GEO: CARCEL 6
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                <<R_int_G>> <<R_ext_G>>
         MIX  3 4 5 6   7 8 1 ;

  ::: C0202 := GEO: C0201
         MIX  9 10 11 12   7 8 1 ;

  ::: C0301 := GEO: C0201
         MIX  13 14 15 16   7 8 1 ;

  ::: C0302 := GEO: C0201
         MIX  17 18 19 20   7 8 1 ;

  ::: C0303 := GEO: C0201
         MIX  21 22 23 24   7 8 1 ;

  ::: C0402 := GEO: C0201
         MIX  26 27 28 29   7 8 1 ;

  ::: C0403 := GEO: C0201
         MIX  30 31 32 33   7 8 1 ;

  ::: C0501 := GEO: C0201
         MIX  34 35 36 37   7 8 1 ;

  ::: C0502 := GEO: C0201
         MIX  38 39 40 41   7 8 1 ;

  ::: C0503 := GEO: C0201
         MIX  42 43 44 45   7 8 1 ;

  ::: C0504 := GEO: C0201
         MIX  46 47 48 49   7 8 1 ;

  ::: C0505 := GEO: C0201
         MIX  50 51 52 53   7 8 1 ;

  ::: C0601 := GEO: C0201
         MIX  54 55 56 57   7 8 1 ;

  ::: C0602 := GEO: C0201
         MIX  58 59 60 61   7 8 1 ;

  ::: G0603 := GEO: CARCEL 8
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<Rcomb1Gad>> <<Rcomb2Gad>> <<Rcomb3Gad>> 
                    <<Rcomb4Gad>> <<Rcomb5Gad>> <<Rcomb6Gad>> 
                    <<R_int_G>> <<R_ext_G>>
         MIX  62 63 64 65 66 67   7 8 1 ;

  ::: C0604 := GEO: C0201
         MIX  68 69 70 71   7 8 1 ;

  ::: C0605 := GEO: C0201
         MIX  72 73 74 75   7 8 1 ;

  ::: C0702 := GEO: C0201
         MIX  76 77 78 79   7 8 1 ;

  ::: C0703 := GEO: C0201
         MIX  80 81 82 83   7 8 1 ;

  ::: C0705 := GEO: C0201
         MIX  84 85 86 87   7 8 1 ;

  ::: C0706 := GEO: C0201
         MIX  88 89 90 91   7 8 1 ;

  ::: G0707 := GEO: G0603
         MIX  92 93 94 95 96 97   7 8 1 ;

  ::: C0801 := GEO: C0201
         MIX  98 99 100 101   7 8 1 ;

  ::: C0802 := GEO: C0201
         MIX  102 103 104 105   7 8 1 ;

  ::: C0803 := GEO: C0201
         MIX  106 107 108 109   7 8 1 ;

  ::: C0804 := GEO: C0201
         MIX  110 111 112 113   7 8 1 ;

  ::: C0805 := GEO: C0201
         MIX  114 115 116 117   7 8 1 ;

  ::: C0806 := GEO: C0201
         MIX  118 119 120 121   7 8 1 ;

  ::: C0807 := GEO: C0201
         MIX  122 123 124 125   7 8 1 ;

  ::: C0808 := GEO: C0201
         MIX  126 127 128 129   7 8 1 ;

  ::: C0901 := GEO: CARCEL 6
         MESHX 0.0 <<CoteL>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                <<R_int_G>> <<R_ext_G>>
         MIX  130 131 132 133   7 8 1 ;

  ::: C0902 := GEO: C0901
         MIX  134 135 136 137   7 8 1 ;

  ::: C0903 := GEO: C0901
         MIX  138 139 140 141   7 8 1 ;

  ::: C0904 := GEO: C0901
         MIX  142 143 144 145   7 8 1 ;

  ::: C0905 := GEO: C0901
         MIX  146 147 148 149   7 8 1 ;

  ::: C0906 := GEO: C0901
         MIX  150 151 152 153   7 8 1 ;

  ::: C0907 := GEO: C0901
         MIX  154 155 156 157   7 8 1 ;

  ::: C0908 := GEO: C0901
         MIX  158 159 160 161   7 8 1 ;

  ::: C0909 := GEO: CARCEL 6
         MESHX 0.0 <<CoteL>>
         MESHY 0.0 <<CoteL>>
         RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                <<R_int_G>> <<R_ext_G>>
         MIX  162 163 164 165   7 8 1 ;

  ::: TG := GEO: CARCEL 2
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<R_int_TG>> <<R_ext_TG>>
         MIX 1 25 1 ;

  ::: TI := GEO: CARCEL 2
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<R_int_TI>> <<R_ext_TI>>
         MIX 1 2 1 ;

;

END: ;
QUIT .
