************************************************************************
*  case ASSBLY_CASEB                                                   *
*  295-group 1/8-th assembly in DRAGON -- 1-level scheme               *
*  Author: A. Canbakan                                                 *
************************************************************************

! BEGIN STRUCTURES and MODULES
LINKED_LIST GEOSS GEON2 TRACKSS TRACKN2 LIBRARY LIBRARY2
            SYS FLUX2 EDITION DONNEES LIBHOM BURNUP
            EDIHOM RES_F RES_P RES_L ;
MODULE SYBILT: G2S: SALT: MCCGT: USS: ASM: FLU: EDI: SPH: UTL: DELETE:
       END: EVO: GREP: LIB: ABORT: ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 R_int_TG R_ext_TG R_int_TI R_ext_TI
     R_int_G R_ext_G Cote CoteL RmodeN1 Lame ;
REAL Rcomb1Gad Rcomb2Gad Rcomb3Gad Rcomb4Gad Rcomb5Gad Rcomb6Gad ;
SEQ_ASCII ASSBLY_CASEB :: FILE './ASSBLY_CASEB_g2s.txt' ;
SEQ_ASCII BCOND2_F :: FILE './ASSBLY_CASEB_R_BCOND2.txt' ;
SEQ_ASCII BCOND2_P :: FILE './ASSBLY_CASEB_R_BCOND2_PEAK.txt' ;
SEQ_ASCII BCOND2_L :: FILE './ASSBLY_CASEB_R_BCOND2_LAST.txt' ;
SEQ_ASCII BURN2 :: FILE './ASSBLY_CASEB_BURN2.txt' ;
SEQ_ASCII FIGASB_CASEB :: FILE './FIGASB_CASEB.ps' ;
SEQ_BINARY TF_EXC ;
INTEGER an2d := 8 ;
REAL densur := 20.0 ;
INTEGER istep maxstep istepNext maxautop iAutop := 0 109 0 10 1 ;
REAL delr BUbeg BUend Tbeg Tend := 0.01 0.0 0.0 0.0 0.0 ;
REAL Fuelpwr Keff := 39.0 0.0 ;
REAL BU BUautop  := 0.0 0.0 ;
STRING Library := "DLIB_295" ;
PROCEDURE Geo_SS Geo_N2 Mix_Gad ;
PROCEDURE assertS ;
! END STRUCTURES AND MODULES

! BEGIN GEOMETRY
EVALUATE Rcomb4 := 0.4096 ;
EVALUATE Rcomb1 := 0.5 SQRT Rcomb4 * ;
EVALUATE Rcomb2 := 0.8 SQRT Rcomb4 * ;
EVALUATE Rcomb3 := 0.95 SQRT Rcomb4 * ;
EVALUATE Rcomb6Gad := 0.4096 ;
EVALUATE Rcomb1Gad := 0.2 SQRT Rcomb6Gad * ;
EVALUATE Rcomb2Gad := 0.4 SQRT Rcomb6Gad * ;
EVALUATE Rcomb3Gad := 0.6 SQRT Rcomb6Gad * ;
EVALUATE Rcomb4Gad := 0.8 SQRT Rcomb6Gad * ;
EVALUATE Rcomb5Gad := 0.95 SQRT Rcomb6Gad * ;
EVALUATE R_int_TG := 0.5605 ;
EVALUATE R_ext_TG := 0.6225 ;
EVALUATE R_int_TI := 0.5725 ;
EVALUATE R_ext_TI := 0.6225 ;
EVALUATE R_int_G := 0.4180 ;
EVALUATE R_ext_G := 0.4750 ;
EVALUATE Cote := 1.26 ;
EVALUATE CoteL := 1.302 ;
EVALUATE Lame := CoteL Cote - ;
EVALUATE RmodeN1 := 0.670 ;

GEOSS := Geo_SS :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                      <<R_int_TG>> <<R_ext_TG>> <<R_int_TI>>
                      <<R_ext_TI>> <<R_int_G>> <<R_ext_G>> <<Cote>>
                      <<CoteL>> <<Rcomb1Gad>> <<Rcomb2Gad>>
                      <<Rcomb3Gad>> <<Rcomb4Gad>> <<Rcomb5Gad>>
                      <<Rcomb6Gad>> ;

GEON2 := Geo_N2 :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                      <<R_int_TG>> <<R_ext_TG>> <<R_int_TI>>
                      <<R_ext_TI>> <<R_int_G>> <<R_ext_G>> <<Cote>>
                      <<Lame>> <<Rcomb1Gad>>
                      <<Rcomb2Gad>> <<Rcomb3Gad>> <<Rcomb4Gad>>
                      <<Rcomb5Gad>> <<Rcomb6Gad>> ;

ASSBLY_CASEB FIGASB_CASEB := G2S: GEON2 ;
! END GEOMETRY

! BEGIN DECLARATION
DONNEES := UTL: ::
  CREA
    BURN <<maxstep>> =
           0.0       7.5      15.0      30.0      50.0     75.0    112.5
         150.0     200.0     250.0     375.0     500.0    625.0    750.0
         875.0    1000.0    1250.0    1500.0    1750.0   2000.0   2250.0
        2500.0    2750.0    3000.0    3250.0    3500.0   3750.0   4000.0
        4250.0    4500.0    4750.0    5000.0    5250.0   5500.0   5750.0
        6000.0    6250.0    6500.0    6750.0    7000.0   7250.0   7500.0
        7750.0    8000.0    8250.0    8500.0    8750.0   9000.0   9250.0
        9500.0    9750.0   10000.0   10250.0   10500.0  10750.0  11000.0
       11250.0   11500.0   11750.0   12000.0   12250.0  12500.0  12750.0
       13000.0   13250.0   13500.0   13750.0   14000.0  14250.0  14500.0
       14750.0   15000.0   15250.0   15500.0   16000.0  16500.0  17000.0
       17500.0   18000.0   18500.0   19000.0   19500.0  20000.0  20500.0
       22000.0   24000.0   26000.0   28000.0   30000.0  32000.0  34000.0
       36000.0   38000.0   40000.0   42000.0   44000.0  46000.0  48000.0
       50000.0   52000.0   54000.0   56000.0   58000.0  60000.0  62000.0
       64000.0   66000.0   68000.0   70000.0
;
DONNEES := UTL: DONNEES ::
  CREA
    AUTOP <<maxautop>> =
        1000.0    4000.0    8000.0   10500.0   12000.0  24000.0  36000.0  
       48000.0   60000.0   70000.0
;
! END DECLARATION

! BEGIN TRACKING
! Level One
TRACKSS := SYBILT: GEOSS ::
  EDIT 0
  MAXR 500
  MAXZ  1000000
  TITLE 'TRACKING FOR ASSEMBLY SS'
  QUA2 20 3
  DP01  
;
! MOC tracking for flux calculation
TRACKN2 TF_EXC := SALT: ASSBLY_CASEB ::
  EDIT 3
  ALLG
  TSPC EQW2 <<an2d>> <<densur>> REND
;
TRACKN2 := MCCGT: TRACKN2 TF_EXC ::
  CACB 4 AAC 80 TMT EPSI 1E-5 MCU 10000
;
! END TRACKING

! BEGIN LIBRARY READING
LIBRARY := Mix_Gad :: <<Library>> ;
! END LIBRARY READING

! BEGIN DEPLETION
WHILE istep maxstep < DO

  ! BEGIN FIRST LEVEL FLUX CALCULATION
  EVALUATE istep := istep 1 + ;
  ECHO "istep: " istep "/" maxstep ;

  EVALUATE BUbeg Tbeg := BUend Tend ;
  GREP: DONNEES :: GETVAL 'BURN' <<istep>> >>BUend<< ;
  EVALUATE Tend := BUend Fuelpwr / ;
  ECHO Tbeg ;
  ECHO Tend ;

  ! BEGIN SELF-SHIELDING CALCULATION
  IF istep 1 = THEN
    LIBRARY2 := USS: LIBRARY TRACKSS :: EDIT 1 PASS 3 GRMIN 52
      CALC  REGI W1 U235   ALL
            REGI W1 U236   ALL
            REGI W1 Pu239  ALL
            REGI W1 Pu240  ALL
            REGI W1 Pu241  ALL
            REGI W1 Pu242  ALL

            REGI W1 U235Gd ALL
            REGI W1 U236Gd ALL

            REGI W1 Gd154  ALL
            REGI W1 Gd155  ALL
            REGI W1 Gd156  ALL
            REGI W1 Gd157  ALL
            REGI W1 Gd158  ALL

            REGI W1 Zr90g  ALL
            REGI W1 Zr91g  ALL
      ENDC ;
  ELSE
    GREP: DONNEES :: GETVAL "AUTOP" <<iAutop>> >>BUautop<< ;
    GREP: DONNEES :: GETVAL "BURN" <<istep>> >>BU<< ;
    IF BUautop BU = THEN
      LIBRARY2 := USS: LIBRARY LIBRARY2 TRACKSS :: EDIT 1 PASS 3
        GRMIN 52
        CALC  REGI W1 U235   ALL
              REGI W1 U236   ALL
              REGI W1 Pu239  ALL
              REGI W1 Pu240  ALL
              REGI W1 Pu241  ALL
              REGI W1 Pu242  ALL

              REGI W1 U235Gd ALL
              REGI W1 U236Gd ALL

              REGI W1 Gd154  ALL
              REGI W1 Gd155  ALL
              REGI W1 Gd156  ALL
              REGI W1 Gd157  ALL
              REGI W1 Gd158  ALL

              REGI W1 Zr90g  ALL
              REGI W1 Zr91g  ALL
        ENDC ;
      EVALUATE iAutop := iAutop 1 + ;
    ENDIF ;
  ENDIF ;
  ! END SELF-SHIELDING CALCULATION

  ! BEGIN MOC FLUX CALCULATION
  SYS := ASM: LIBRARY2 TRACKN2 TF_EXC :: ARM EDIT 1 ;

  IF istep 1 = THEN
    FLUX2 := FLU: LIBRARY2 SYS TRACKN2 TF_EXC ::
      EDIT 1 TYPE K ;
  ELSE
    FLUX2 := FLU: FLUX2 LIBRARY2 SYS TRACKN2 TF_EXC ::
      EDIT 1 TYPE K ;
  ENDIF ;

  IF istep 1 = THEN
    RES_F := EDI: FLUX2 LIBRARY2 TRACKN2 :: EDIT 5
      MICR 13 U235 U238 O16 U235Gd U238Gd O16Gd2 Gd154 Gd155
              Gd156 Gd157 Gd158 Pu239 Pu241
      MERG MIX
      COND 0.625
      SAVE ON COND2
    ;
    BCOND2_F := RES_F :: STEP UP COND2 ;
  ENDIF ;

  ! PEAK is at step 54 burnup 10500.0
  IF istep 54 = THEN
    RES_P := EDI: FLUX2 LIBRARY2 TRACKN2 :: EDIT 5
      MICR 13 U235 U238 O16 U235Gd U238Gd O16Gd2 Gd154 Gd155
              Gd156 Gd157 Gd158 Pu239 Pu241
      MERG MIX
      COND 0.625
      SAVE ON COND2
    ;
    BCOND2_P := RES_P :: STEP UP COND2 ;
  ENDIF ;

  IF istep maxstep = THEN
    RES_L := EDI: FLUX2 LIBRARY2 TRACKN2 :: EDIT 5
      MICR 13 U235 U238 O16 U235Gd U238Gd O16Gd2 Gd154 Gd155
              Gd156 Gd157 Gd158 Pu239 Pu241
      MERG MIX
      COND 0.625
      SAVE ON COND2
    ;
    BCOND2_L := RES_L :: STEP UP COND2 ;
  ENDIF ;

  GREP: FLUX2 :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Keff<< ;
  ECHO "+++ Burnup=" BUend " Keff=" Keff ;

  IF istep maxstep < THEN
    EVALUATE istepNext := istep 1 + ;
    GREP: DONNEES :: GETVAL 'BURN' <<istepNext>> >>BUend<< ;
    EVALUATE Tend := BUend Fuelpwr / ;

    IF istep 1 = THEN
      BURNUP LIBRARY2 := EVO: LIBRARY2 FLUX2 TRACKN2 ::
        EDIT 3 DEPL <<Tbeg>> <<Tend>> DAY POWR <<Fuelpwr>>
        NOEX ;
    ELSE
      IF istep 5 < THEN
        BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX2 TRACKN2 ::
          EDIT 3 DEPL <<Tbeg>> <<Tend>> DAY POWR <<Fuelpwr>>
          NOEX ;
      ELSE
        BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX2 TRACKN2 ::
          EDIT 3 DEPL <<Tbeg>> <<Tend>> DAY POWR <<Fuelpwr>>
        ;
      ENDIF ;
    ENDIF ;

    SYS := DELETE: SYS ;

  ENDIF ;
  ! END MOC FLUX CALCULATION

ENDWHILE ;
! END DEPLETION CALCULATION

BURN2 := BURNUP ;

assertS FLUX2 :: K-EFFECTIVE 1 0.7441179 ;

ECHO "test ASSBLY_CASEB_1level completed" ;
END: ;
