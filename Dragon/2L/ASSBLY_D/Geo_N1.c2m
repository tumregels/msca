*DECK Geo_N1
*----
*  Name          : Geo_N1.c2m
*  Type          : DRAGON procedure
*  Use           : Geometry generation for 1st Level Flux Calculation
*                  of the CASED assembly
*  Author        : A. Canbakan
*
*  Procedure called as:
*
*GEON1 := Geo_N1 :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
*                   <<R_int_TG>> <<R_ext_TG>> <<R_int_TI>>
*                   <<R_ext_TI>> <<R_int_G>> <<R_ext_G>> <<Cote>>
*                   <<CoteL>> <<RmodeN1>> ;
*
*  Input data   :
*    Rcomb1     :  50/100 of outer radius of fuel (cm)
*    Rcomb2     :  80/100 of outer radius of fuel (cm)
*    Rcomb3     :  95/100 of outer radius of fuel (cm)
*    Rcomb4     : 100/100 of outer radius of fuel (cm)
*    R_int_TG   : Inner radius of cladding of guide tube (cm)
*    R_ext_TG   : Outer radius of cladding of guide tube (cm)
*    R_int_TI   : Inner radius of cladding of guide tube (cm)
*    R_ext_TI   : Outer radius of cladding of guide tube (cm)
*    R_int_G    : Inner radius of cladding of fuel tube (cm)
*    R_ext_G    : Outer radius of cladding of fuel tube (cm)
*    Cote       : Lattice pitch (cm)
*    CoteL      : Lattice pitch + Water space (cm)
*    RmodeN1    : Parameter for flux calculation in Level 1 (cm)
*
*  Output data  :
*    GEON1      : Geometry for 1st Level Flux Calculation


PARAMETER  GEON1  ::
       EDIT 0
           ::: LINKED_LIST GEON1  ;
   ;
*----
*  Modules used in this procedure
*----
MODULE  GEO: END: ;

*----
*  Input data recovery
*----
*
REAL Rcomb1       Rcomb2       Rcomb3       Rcomb4        ;
:: >>Rcomb1<<   >>Rcomb2<<   >>Rcomb3<<   >>Rcomb4<<      ;
REAL R_int_TG     R_ext_TG     R_int_TI     R_ext_TI      ;
:: >>R_int_TG<< >>R_ext_TG<< >>R_int_TI<< >>R_ext_TI<<    ;
REAL R_int_G      R_ext_G      Cote         CoteL         ;
:: >>R_int_G<<  >>R_ext_G<<  >>Cote<<     >>CoteL<<       ;
REAL RmodeN1      Rcomb1Gad     Rcomb2Gad     Rcomb3Gad   ;
:: >>RmodeN1<<  >>Rcomb1Gad<< >>Rcomb2Gad<< >>Rcomb3Gad<< ;
REAL Rcomb4Gad       Rcomb5Gad       Rcomb6Gad            ;
:: >>Rcomb4Gad<<   >>Rcomb5Gad<<   >>Rcomb6Gad<<          ;

GEON1 := GEO: :: CAR2D 9 9
  EDIT 0
  X- DIAG X+ REFL
  Y- SYME Y+ DIAG
  CELL TI C1 G1 TG C1 G3 TG C4 C6
          C2 C2 C1 C2 C2 C1 C2 C6
             C2 C1 C2 C2 C1 C2 C6
                TG C1 C1 TG G5 C6
                   G2 C1 C1 C2 C6
                      TG C1 C3 C6
                         G4 C3 C6
                            C5 C7
                               C8

  MERGE 11 1  13 10  1  15  9  4  6
           2   2  1  2   2  1  2  6
              12  1  2   2  1  2  6
                 10  1   1  9 17  6
                    14   1  1  2  6
                         9  1  3  6
                           16  3  6
                               5  7
                                  8

  TURN  A  A  A  A  A  A  A  A  A
           A  E  F  A  E  D  A  A
              C  B  G  C  H  G  A
                 A  G  C  A  A  A
                    A  B  D  A  A
                       A  A  A  A
                          A  G  A
                             A  A
                                A


  ::: G1 := GEO: CARCEL 9
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<Rcomb1Gad>> <<Rcomb2Gad>> <<Rcomb3Gad>>
         <<Rcomb4Gad>> <<Rcomb5Gad>> <<Rcomb6Gad>>
         <<R_int_G>> <<R_ext_G>> <<RmodeN1>>
         MIX  13 14 15 16 17 18  7 8 1 1
  ;

  ::: G2 := GEO: G1
         MIX  52 53 54 55 56 57  7 8 1 1
  ;

  ::: G3 := GEO: G1
         MIX  58 59 60 61 62 63  7 8 1 1
  ;
  ::: G4 := GEO: G1
         MIX  96 97 98 99 100 101  7 8 1 1
  ;
  ::: G5 := GEO: G1
         MIX  114 115 116 117 118 119  7 8 1 1
  ;

  ::: C1 := GEO: CARCEL 7
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                <<R_int_G>> <<R_ext_G>> <<RmodeN1>>
         MIX  3 4 5 6   7 8 1 1
  ;
  ::: C2 := GEO: C1
         MIX 9 10 11 12   7 8 1 1
  ;
  ::: C3 := GEO: C1
         MIX 124 125 126 127  7 8 1 1
  ;
  ::: C4 := GEO: C1
         MIX 102 103 104 105  7 8 1 1
  ;
  ::: C5 := GEO: C1
         MIX 132 133 134 135  7 8 1 1
  ;
  ::: C6 := GEO: CARCEL 7
         MESHX 0.0 <<CoteL>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                <<R_int_G>> <<R_ext_G>> <<RmodeN1>>
         MIX 136 137 138 139  7 8 1 1
  ;
  ::: C7 := GEO: C6
         MIX 164 165 166 167  7 8 1 1
  ;
  ::: C8 := GEO: CARCEL 7
         MESHX 0.0 <<CoteL>>
         MESHY 0.0 <<CoteL>>
         RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                <<R_int_G>> <<R_ext_G>> <<RmodeN1>>
         MIX 168 169 170 171  7 8 1 1
  ;
  ::: TG := GEO: CARCEL 2
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<R_int_TG>> <<R_ext_TG>>
         MIX 1 27 1
  ;
  ::: TI := GEO: CARCEL 2
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<R_int_TI>> <<R_ext_TI>>
         MIX 1 2 1
  ;
;

END: ;
QUIT .
