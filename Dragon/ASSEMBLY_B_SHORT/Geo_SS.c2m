*DECK Geo_SS
*----
*  Name          : Geo_SS.c2m
*  Type          : DRAGON procedure
*  Use           : Geometry generation for Self-Shielding Calculation
*                  with 37 fuel regions
*  Author        : A. Canbakan
*
*  Procedure called as: 
*
*GEOSS := Geo_SS :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
*                   <<R_int_TG>> <<R_ext_TG>> <<R_int_TI>> 
*                   <<R_ext_TI>> <<R_int_G>> <<R_ext_G>> <<Cote>>
*                   <<CoteL>> ;
*
*  Input data   :
*    Rcomb1     :  50/100 of outer radius of fuel (cm)
*    Rcomb2     :  80/100 of outer radius of fuel (cm)
*    Rcomb3     :  95/100 of outer radius of fuel (cm)
*    Rcomb4     : 100/100 of outer radius of fuel (cm)
*    R_int_TG   : Inner radius of cladding of guide tube (cm)
*    R_ext_TG   : Outer radius of cladding of guide tube (cm)
*    R_int_TI   : Inner radius of cladding of instrumentation tube (cm)
*    R_ext_TI   : Outer radius of cladding of instrumentation tube (cm)
*    R_int_G    : Inner radius of cladding of fuel tube (cm)
*    R_ext_G    : Outer radius of cladding of fuel tube (cm)
*    Cote       : Lattice pitch (cm)
*    CoteL      : Lattice pitch + Water space (cm)
*
*  Output data  :
*    GEOSS      : Geometry for Self-Shielding Calculation


PARAMETER  GEOSS  ::  
       EDIT 0 
           ::: LINKED_LIST GEOSS  ; 
   ;
*----
*  Modules used in this procedure
*----
MODULE  GEO: END: ;

*----
*  Input data recovery
*----
*                                                                      ;
REAL Rcomb1       Rcomb2       Rcomb3       Rcomb4     ;
:: >>Rcomb1<<   >>Rcomb2<<   >>Rcomb3<<   >>Rcomb4<<   ;
REAL R_int_TG     R_ext_TG     R_int_TI     R_ext_TI   ;
:: >>R_int_TG<< >>R_ext_TG<< >>R_int_TI<< >>R_ext_TI<< ;
REAL R_int_G      R_ext_G      Cote         CoteL      ;
:: >>R_int_G<<  >>R_ext_G<<  >>Cote<<     >>CoteL<<    ;
REAL   Rcomb1Gad     Rcomb2Gad     Rcomb3Gad   ;
::   >>Rcomb1Gad<< >>Rcomb2Gad<< >>Rcomb3Gad<< ;
REAL Rcomb4Gad       Rcomb5Gad       Rcomb6Gad            ;
:: >>Rcomb4Gad<<   >>Rcomb5Gad<<   >>Rcomb6Gad<<          ;



GEOSS := GEO: :: CAR2D 9 9
  EDIT 5
  X- DIAG X+ REFL
  Y- SYME Y+ DIAG
  CELL TI C1 C1 T1 C1 C1 T2 C4 C6
          C2 C2 C1 C2 C2 C1 C2 C6
             C2 C1 C2 G1 C1 C2 C6
                T1 C1 C1 T2 C4 C6
                   C2 C1 C1 C2 C6
                      T2 C1 C3 C6
                         C2 C3 C6
                            C5 C7
                               C8

  MERGE 11 1  1 10  1  1  9  4  6
           2  2  1  2  2  1  2  6
              2  1  2 12  1  2  6
                10  1  1  9  4  6
                    2  1  1  2  6
                       9  1  3  6
                          2  3  6
                             5  7
                                8

* T2 -> 9 , T1 ->  10 , TI -> 11

  TURN  A  A  E  A  A  E  A  A  A
           A  E  F  A  E  D  A  A
              C  B  G  C  H  G  A
                 A  G  C  A  G  A
                    A  B  D  A  A
                       A  A  A  A
                          A  G  A
                             A  A
                                A


  ::: G1 := GEO: CARCEL 8
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<Rcomb1Gad>> <<Rcomb2Gad>> <<Rcomb3Gad>> 
         <<Rcomb4Gad>> <<Rcomb5Gad>> <<Rcomb6Gad>> <<R_int_G>> 
         <<R_ext_G>>           
         MIX  21 22 23 24 25 26   7 8 1
  ; 
  ::: C1 := GEO: CARCEL 6
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                <<R_int_G>> <<R_ext_G>>
         MIX  3 4 5 6   7 8 1
  ;
  ::: C2 := GEO: C1
         MIX 9 10 11 12   7 8 1
  ;
  ::: C3 := GEO: C1
         MIX 120 121 122 123   7 8 1
  ;
  ::: C4 := GEO: C1
         MIX 100 101 102 103   7 8 1
  ;
  ::: C5 := GEO: C1
         MIX 128 129 130 131   7 8 1
  ;
  ::: C6 := GEO: CARCEL 6
         MESHX 0.0 <<CoteL>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                <<R_int_G>> <<R_ext_G>>
         MIX 132 133 134 135   7 8 1
  ;
  ::: C7 := GEO: C6
         MIX 160 161 162 163   7 8 1
  ;
  ::: C8 := GEO: CARCEL 6
         MESHX 0.0 <<CoteL>>
         MESHY 0.0 <<CoteL>>
         RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                <<R_int_G>> <<R_ext_G>>
         MIX  68  69  70  71   7 8 1
  ;
  ::: T2 := GEO: CARCEL 2
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<R_int_TG>> <<R_ext_TG>>
         MIX 1 27 1
  ;
  ::: T1 := GEO: T2
         MIX 1 27 1
  ;
  ::: TI := GEO: CARCEL 2
         MESHX 0.0 <<Cote>>
         MESHY 0.0 <<Cote>>
         RADIUS 0.0 <<R_int_TI>> <<R_ext_TI>>
         MIX 1 2 1
  ;
;

END: ;
QUIT .
